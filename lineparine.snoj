## lineparine orthography
## with preprocess
## vowel "u" -> ("a" | "e" | "o") "u"
## added punctuation

% preprocess {
	@FALL_THROUGH;
	("1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0") -> /%/
}

% font_transcription => IPA {
@CASE_SENSITIVE;
vowel = "a" | "i" | "u" | "e" | "o" | "y"
palat = "X" | "x" | "ch" | "sh"
PUNCTUATION = "." | "," | "!" | "?" | "(" | ")" | "/" | "\"" | "<" | ">" | "―" | "[" | "]" | "…" | "=" | ":" | "─" | "─" | "~" | "^"
strong_vowel = "a" | "e" | "o"
pure_consonant = "p"  | "b"  | "t"  | "d"  | "k"  | "g"  | "z"  | "m"  | "n"  | "f"  | "v"  | "c"  | "s"  | "x"  | "h"   | "l"  | "r"  | "ts"  | "ch"  | "dz"  | "ng"  | "fh"  | "vh"  | "sh"  | "th"  | "dh"  | "kh" | "rkh" | "rl"  | "ph"

^ "-" -> /-/
"-" ^ -> /-/
"-" -> //

^ "edioll" ^ -> /edjol/
^ "wioll" ^ -> /wjol/
^ "woll" ^ -> /wol/
"vsfafgh" -> /vusfafuguh/
"simfgh" -> /zimfuguh/

"sh" -> /ʃ/

pure_consonant "i" "y" !("'i" | "'j" | "i" | "j") -> $ /i/ /ɥ/ 

("a" | "u" | "e" | "o" | "y") "y" ("i" | "j") vowel -> $ /ɥ/ /j/ $
"y" ("i" | "j") -> /y/ /j/

palat "i" vowel -> $ // $
strong_vowel ("i" | "y") "i" !vowel -> $ $ /i/
strong_vowel ("i" | "y") "'" "i" !vowel -> $ $ $ /i/
vowel "i" !"r" -> $ /j/
vowel "'" "i" !"r" -> $  $ /j/
palat "i" "u" -> $ $ /u/
strong_vowel "u" !"r" -> $ /w/
strong_vowel "'" "u" !"r" -> $ $ /w/

"ni" vowel -> /ɲ/ $;
"nj" vowel -> /ɲ/ $;
"i" vowel -> /j/ $
## added by https://twitter.com/sashimiwiki/status/935493190504759299
"u" vowel -> /w/ $;

"s" "j" !vowel -> /z/ $;
"s" !vowel -> /s/
!vowel "j" !vowel -> /i/

vowel "y" !vowel -> $ /ɥ/
!vowel ("rkh" | "Rkh") -> /ʁ/


vowel "r" -> $ /ː/
"rl" -> /ɹ/
"Rl" -> /ɹ/
!vowel ("r" | "R") -> /r/

"y" vowel -> /ɥ/ $




"tz" -> /ʦʦ/;
"sx" -> /ʃʃ/;
"dX" -> /ʥʥ/;

"ts" -> /ʦ/
"ch" -> /ʧ/
"ng" -> /ŋ/

"th" -> /θ/
"dh" -> /ð/
"kh" -> /x/



"ph" -> /p/



"a" -> /a/; "i" -> /i/; "u" -> /u/; "e" -> /e/; "o" -> /o/; "y" -> /y/

"p" -> /p/; "b" -> /b/; "t" -> /t/; "d" -> /d/; "k" -> /k/; "g" -> /g/;
"m" -> /m/; "n" -> /n/; "f" -> /f/; "v" -> /v/; 
"c" -> /s/; "z" -> /ʦ/;
"s" -> /z/ 
"x" -> /ʃ/
"h" -> /h/
"w" -> /w/
"j" -> /j/
"l" -> /l/
"q" -> /kw/

"'" -> //

"X" -> /ʥ/
"F"  -> /ɸ/
"V"  -> /β/

# "l" !vowel -> /ʟ/

"%" -> /%/

}

% normal_transcription => font_transcription {
@FALL_THROUGH;
	"fh" -> /F/
	"vh" -> /V/
	"dz" -> /X/
	# does not handle r, since it will be correctly handled in the next module
}

%% preprocess >> normal_transcription => font_transcription => IPA;
